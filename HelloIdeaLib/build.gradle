buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android-library'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
        versionCode 1
        versionName '0.0.1'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:+'
}

/*
 * JUnit test support
 */

// extend the runtime
configurations {
    unitCompile.extendsFrom runtime
    unitRuntime.extendsFrom unitCompile
}

// add to dependancies
dependencies {
    unitCompile files("$project.buildDir/classes/release")
    unitCompile 'junit:junit:4.11'
}

// add a new unit sourceSet
sourceSets {
    unit {
        java.srcDir file('src/unit/java/**')
        resources.srcDir file('src/unit/resources/**')
    }
}


// add the unit task
task unit(type:Test, dependsOn: assemble) {
    description = "run unit tests"
    testClassesDir = project.sourceSets.unit.output.classesDir
    classpath = project.sourceSets.unit.runtimeClasspath
}

// bind to check
check.dependsOn unit

/*
 * Publishing to local mvn repo
 */
version '0.0.1'
group 'com.karolmajta'

publishing {
    publications {
        maven(MavenPublication) {
            artifact file("${project.buildDir}/libs/${project.name}-${version}.aar")
        }
    }
}

publishToMavenLocal.dependsOn build